#!meta

{"kernelInfo":{"defaultKernelName":"csharp","items":[{"aliases":[],"name":"csharp"},{"aliases":[],"languageName":"fsharp","name":"fsharp"}]}}

#!markdown

# 23 a maze

#!fsharp

#load "Aoc.fsx"

let input = Aoc.fetchInput 23

let testInput = "#.#####################
#.......#########...###
#######.#########.#.###
###.....#.>.>.###.#.###
###v#####.#v#.###.#.###
###.>...#.#.#.....#...#
###v###.#.#.#########.#
###...#.#.#.......#...#
#####.#.#.#######.#.###
#.....#.#.#.......#...#
#.#####.#.#.#########v#
#.#...#...#...###...>.#
#.#.#v#######v###.###v#
#...#.>.#...>.>.#.###.#
#####v#.#.###v#.#.###.#
#.....#...#...#.#.#...#
#.#########.###.#.#.###
#...###...#...#...#.###
###.###.#.###v#####v###
#...#...#.#.>.>.#.>.###
#.###.###.#.###.#.#v###
#.....###...###...#...#
#####################.#
"

#!markdown

## parse!

another grid type with fast neighbor lookup. could be a 2D array.

#!fsharp

type Direction = Up | Down | Left | Right
type Tile = Path | Forest | Slope of Direction
type Point = int * int
type Grid = Map<Point, Tile>

let parse (input: string) : Grid =
  seq { for y, line in Array.indexed <| input.Split('\n') do
          for x, c in Seq.indexed line do
            (x,y),
            match c with
            | '#' -> Forest
            | '.' -> Path
            | '>' -> Slope Right
            | '<' -> Slope Left
            | '^' -> Slope Up
            | 'v' -> Slope Down
            | _ -> failwith "invalid input" } |> Map.ofSeq

parse testInput

#!fsharp

module Part1 =
  let neighbors (x,y) (grid: Grid) : Point list =

    let choose (direction, pt) =
      Map.tryFind pt grid
      |> function
        | Some (Slope slopeDirection) when direction = slopeDirection -> Some pt
        | Some Path -> Some pt
        | _ -> None

    [ Left, (x-1,y); Right, (x+1,y); Up, (x,y-1); Down, (x,y+1) ]
    |> List.choose choose

#!fsharp

let rows : Grid -> int =
  Map.toSeq >> Seq.map (fst >> snd) >> Seq.max

let print (grid: Grid) (path: Point Set) =
  let rows = rows grid
  let cols = grid |> Map.toSeq |> Seq.map (fst >> fst) |> Seq.max
  seq { for y in 0..rows do
          System.Environment.NewLine
          for x in 0..cols do
            match Map.tryFind (x,y) grid with
            | _ when Set.contains (x,y) path -> "O" 
            | Some Forest -> "#"
            | Some Path -> "."
            | Some (Slope Right) -> ">"
            | Some (Slope Left) -> "<"
            | Some (Slope Up) -> "^"
            | Some (Slope Down) -> "v"
            | None -> " "
  } |> String.concat "" |> printfn "%s"

let findStartStop grid =
  let lastRow = rows grid

  let findStartStop ((start, stop): Point option * Point option) : Point*Tile -> Point option * Point option =
    function
    | ((_, 0) as pt, Path) when start = None -> Some pt, stop
    | ((_, y) as pt, Path) when stop = None && y = lastRow -> start, Some pt
    | _ -> start, stop

  grid
  |> Map.toSeq
  |> Seq.fold findStartStop (None, None)
  |> function
     | Some start, Some stop -> Some (start,stop)
     | _ -> None

#!fsharp

module Part1 =
  let traverse start (grid: Grid) =
    let neighbors (pt: Point) : Point Set = grid |> Part1.neighbors pt |> Set.ofList

    let rec loop completed : (Point*Point Set) list -> (Point Set) list =
      function
      | [] -> completed
      | (start,path) :: paths ->
        let visited = Set.add start path
        match path |> Set.difference (neighbors start) |> Set.toList with
        | [] -> loop (visited::completed) paths
        | neighbor :: neighbors -> loop completed ((neighbor, visited) :: (paths @ [for neighbor in neighbors -> neighbor, visited]))

    loop [] [start, Set.empty]

  let solve grid =
    let map (start,stop) =
      (grid |> traverse start |> List.filter (Set.contains stop) |> List.map Set.count |> List.max) - 1
    grid |> findStartStop |> Option.map map

#!fsharp

let testGrid = parse testInput

Part1.solve testGrid

#!fsharp

input |> parse |> Part1.solve

#!markdown

## Part 2

More possible paths.

Ok. Not to be too hammer so everything is a nail, but isn't this a case for a _reverse_ dijksta? like, a priority queue that will return the longest path to any point?

#!fsharp

module Part2 =
  let neighbors (x,y) (grid: Grid) : Point list =
    let choose pt =
      Map.tryFind pt grid
      |> function
        | Some (Slope _) | Some Path -> Some pt
        | _ -> None

    List.choose choose [x-1,y; x+1,y; x,y-1; x,y+1]

#!fsharp

type 'a PQ = {
  sortWith: 'a -> 'a -> int
  list: 'a list
}

module PQ =
  let empty sortWith = {sortWith = sortWith; list = []}
  let add (item: 'a) (pq: 'a PQ) = {pq with list = List.sortWith pq.sortWith (item::pq.list)}
  let pop (pq: 'a PQ) : 'a option * 'a PQ = 
     match pq.list with
     | head::tail -> Some head, {pq with list = tail}
     | [] -> None, pq

#!fsharp

let pq = [5;2;7] |> List.fold (fun pq i -> PQ.add i pq) (PQ.empty (fun x y -> x - y))

PQ.pop pq

#!fsharp

type Weights = Map<int, Point list> * Map<Point, int>

module Weights =
  let empty : Weights = Map.empty, Map.empty

  let add weight point ((pq, map) : Weights) : Weights =
    let change =
      function
      | Some points -> List.distinct (point::points)
      | None -> [point]
      >> Some
    (Map.change weight change pq, Map.add point weight map)

  let tryFind (pt: Point) ((_, map):Weights) : int option = Map.tryFind pt map

  let rec private pop f ((pq, map) as weights) =
    if Map.isEmpty pq then None, weights
    else
      match f pq with
      | weight, point::rest -> (Some (weight, point)), (Map.add weight rest pq, Map.remove point map)
      | weight, [] -> pop f (Map.remove weight pq, map)

  let popMax: Weights -> ((int*Point) option)*Weights = pop Map.maxKeyValue
  let popMin: Weights -> ((int*Point) option)*Weights = pop Map.minKeyValue

#!fsharp

module Part2 =
  let traverse (start: Point) (stop: Point) (grid: Grid) =
    let neighbors (pt: Point) : Point list = Part2.neighbors pt grid

    let rec loop (path: Map<Point,Point>) =
      Weights.popMin
      >> function
      | Some (currentWeight, point), weights ->
        let updateWeights ((path, weights) as acc) (neighbor: Point) =
          match Weights.tryFind neighbor weights with
          | Some previousWeight ->
              let nextWeight = currentWeight + 1
              if nextWeight < previousWeight then
                Map.add neighbor point path, Weights.add nextWeight neighbor weights
              else acc
          | None -> acc

        let path, weights = point |> neighbors |> List.fold updateWeights (path, weights)

        loop path weights
      | None, _ -> path
        

    let buildWeights acc point tile =
      match tile with
      | Path | Slope _ -> Weights.add (if point = start then 0 else System.Int32.MaxValue) point acc
      | _ -> acc

    let weights = Map.fold buildWeights Weights.empty grid

    loop Map.empty weights

  let buildPath start stop path =
    let rec loop =
      function
      | [] -> []
      | (head::_) as p ->
        match Map.tryFind head path with
        | Some nextPoint -> loop (nextPoint::p)
        | None -> p

    loop [stop]

  let solve grid =
    let map (start, stop) =
      let path = grid |> traverse start stop |> buildPath start stop
      path |> Set.ofList |> print grid
      List.length path - 1
    grid |> findStartStop |> Option.map map

#!fsharp

testInput |> parse |> Part2.solve

// 1938 is too low
// 4810 is too low
